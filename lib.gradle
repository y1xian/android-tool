def runAsApp = ext.has('runAsApp')
//是否kt
def isKt = ext.has('isKt')
ext.isKt = isKt

if (runAsApp) {
    runAsApp = ext.runAsApp
} else if (ext.has('mainApp') && ext.mainApp) { //ext.mainApp为true时，代表以app方式运行
    runAsApp = true
}

//设置到ext中，供module的build.gradle使用（例如用于设置sourceSets配置）
ext.runAsApp = runAsApp
if (runAsApp) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.y1xian'
}
if (isKt) {
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
}

//默认配置了AndroidManifest.xml在library模式和application模式下的文件路径
android {
    //版本配置
    compileSdkVersion build_version.compileSdkVersion
//    buildToolsVersion build_version.buildToolsVersion
    defaultConfig {
        minSdkVersion build_version.minSdkVersion
        targetSdkVersion build_version.targetSdkVersion
        versionCode build_version.versionCode
        versionName build_version.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        debug {
            //混淆
            minifyEnabled false
//            applicationIdSuffix ".debug"
//            versionNameSuffix "-DEV"
//            //停用 Crashlytics（Fabric）
//            ext.enableCrashlytics = false
            //是否清理无用资源
            shrinkResources false
            //是否启用zipAlign压缩
            zipAlignEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            //混淆
            minifyEnabled false
            //是否清理无用资源
            shrinkResources false
            //是否启用zipAlign压缩
            zipAlignEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    if (isKt) {
        //Kotlin中使用Java8特性
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    //防止编译的时候oom、GC
    dexOptions {
        preDexLibraries false
        maxProcessCount 8
        //分配给gradle的内存
        javaMaxHeapSize "4g"
    }

    lintOptions {
        // true--关闭lint报告的分析进度
//        quiet false
        //  true--所有正式版构建执行规则生成崩溃的lint检查，如果有崩溃问题将停止构建
        checkReleaseBuilds false
        // true--错误发生后停止gradle构建
        abortOnError false
    }

    aaptOptions {
        //停用 PNG 处理
        cruncherEnabled false
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    if (runAsApp) {
        androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        testImplementation libs.junit
        androidTestImplementation libs.test_runner
        androidTestImplementation libs.test_espresso
    }

    if (isKt) {
        api libs.kotlin_stdlib, libs.kotlin_coroutines, libs.kotlin_coroutines_core
    }

}


